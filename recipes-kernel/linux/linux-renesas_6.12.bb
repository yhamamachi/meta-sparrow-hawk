SUMMARY = "Linux kernel v6.12"
DESCRIPTION = "Linux kernel v6.12 for the R-Car board"
HOMEPAGE = "https://github.com/rcar-community/linux"
BUGTRACKER = "https://github.com/orgs/rcar-community/discussions/categories/q-a"
SECTION = "kernel"
LICENSE = "GPLv2-only"
LIC_FILES_CHKSUM = "file://COPYING;md5=6bc538ed5bd9a7fc9398086aedcd7e46"

# nooelint: oelint.file.requirenotfound - This file is provided by poky
require recipes-kernel/linux/linux-yocto.inc
CVE_PRODUCT ?= ""

# LINUX_VERSION/REPO/BRANCH/SRCREV are defined in inc file
require recipes-kernel/linux/kernel_6.12.inc
COMPATIBLE_MACHINE = "(rcar-gen3|rcar-gen4)"

# nooelint: oelint.vars.mispell.unknown - Yocto variable
KCONFIG_MODE = "alldefconfig"
# nooelint: oelint.vars.mispell.unknown - Use in tree defconfig
KBUILD_DEFCONFIG:sparrow-hawk = "renesas_defconfig"
PV = "${LINUX_VERSION}+git${SRCPV}"
SRC_URI = "${REPO};branch=${BRANCH};protocol=https"
SRC_URI:append:sparrow-hawk = " \
    file://sparrow_hawk.cfg \
    file://0001-arm64-dts-renesas-sparrow-hawk-Enable-I2C3-I2C4.patch \
    file://0002-HACK-drivers-gpu-drm-drm_file-Ingnore-flag-checking.patch \
"
# Patchset for power management
SRC_URI:append:sparrow-hawk = " \
    file://0001-drivers-clk-r8a779g0-cpg-mssr-backport-from-BSP-.patch \
    file://0001-driver-pmdomain-r8a779g0-Backport-update.patch \
"
# UIO driver patchset
SRC_URI:append:sparrow-hawk = " \
    file://uio/0001-uio-Add-new-ioctl-for-power-management.patch \
    file://uio/0002-uio-Add-PMA-IOCTL-to-compat-list.patch \
    file://uio/0003-uio-uio_pdrv_genirq-renesas-Add-clock-divisor-ioctl-.patch \
    file://uio/0004-uio-Fix-the-logic-of-setting-reset-to-UIO-devices.patch \
    file://uio/0005-uio-Initialize-reset-struct-of-each-UIO-device.patch \
    file://uio/0006-uio-Inform-UIO-no-reset-line-to-users.patch \
    file://uio/0007-uio-Use-EOPNOTSUPP-not-ENOTSUPP.patch \
    file://uio/0008-uio-Switch-to-clk_hw_get_flags.patch \
    file://uio/0009-uio-Support-error-notification-to-upper-layer.patch \
    file://uio/0010-uio-uio_pdrv_genirq-Add-parameter-to-check-device-no.patch \
    file://uio/0011-uio-uio_pdrv_genirq-Add-parameter-error-in-case-not-.patch \
    file://uio/0012-uio-uio_pdrv_genirq-Hotfix-clock-and-power-control-f.patch \
    file://uio/0013-Revert-uio-uio_pdrv_genirq-Add-parameter-error-in-ca.patch \
    file://uio/0014-uio-uio_pdrv_genirq-Add-parameter-error-in-case-not-.patch \
    file://uio/0015-WIP-Fix-build-error-on-kernel-6.12.patch \
"
S = "${WORKDIR}/git"

KERNEL_DEVICETREE:append:sparrow-hawk = " \
    renesas/r8a779g3-sparrow-hawk-camera-j1-imx219.dtbo \
    renesas/r8a779g3-sparrow-hawk-camera-j2-imx219.dtbo \
    renesas/r8a779g3-sparrow-hawk-camera-j1-imx462.dtbo \
    renesas/r8a779g3-sparrow-hawk-camera-j2-imx462.dtbo \
    renesas/r8a779g3-sparrow-hawk-camera-j1-imx708.dtbo \
    renesas/r8a779g3-sparrow-hawk-camera-j2-imx708.dtbo \
    renesas/r8a779g3-sparrow-hawk-fan-pwm.dtbo \
    renesas/r8a779g3-sparrow-hawk-fan-argon40.dtbo \
    renesas/r8a779g3-sparrow-hawk-rpi-display-2-5in.dtbo \
    renesas/r8a779g3-sparrow-hawk-rpi-display-2-7in.dtbo \
    renesas/r8a779g3-sparrow-hawk-ws-display-13in.dtbo \
    renesas/r8a779g3-sparrow-hawk-olimex-dsi-hdmi.dtbo \
"

BBCLASSEXTEND ?= ""

# uio_pdrv_genirq configuration
KERNEL_MODULE_AUTOLOAD:append = " uio_pdrv_genirq"
KERNEL_MODULE_PROBECONF:append = " uio_pdrv_genirq"
# nooelint: oelint.vars.mispell.unknown - This is general format for module_conf
module_conf_uio_pdrv_genirq:append = " options uio_pdrv_genirq of_id=\"generic-uio\""

do_compile_kernelmodules:append () {
    if (grep -q -i -e '^CONFIG_MODULES=y$' ${B}/.config); then
        # 5.10+ kernels have module.lds that we need to copy for external module builds
        if [ -e "${B}/scripts/module.lds" ]; then
            install -Dm 0644 ${B}/scripts/module.lds ${STAGING_KERNEL_BUILDDIR}/scripts/module.lds
        fi
    fi
}

do_deploy:append() {
    # Remove the redundant device tree file (<device_tree>-<MACHINE>.dtb) that was created in the deploy directory
    for dtbf in ${KERNEL_DEVICETREE}; do
        dtb=`normalize_dtb "$dtbf"`
        dtb_ext=${dtb##*.}
        dtb_base_name=`basename $dtb .$dtb_ext`
        rm -f $deployDir/$dtb_base_name-${KERNEL_DTB_LINK_NAME}.$dtb_ext
    done
}
