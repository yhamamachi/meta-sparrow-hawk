DESCRIPTION = "Linux kernel for the R-Car board"

require recipes-kernel/linux/linux-yocto.inc

# LINUX_VERSION/REPO/BRANCH/SRCREV are defined in inc file
require recipes-kernel/linux/kernel_6.12.inc

COMPATIBLE_MACHINE = "(rcar-gen3|rcar-gen4)"

#KERNEL_VERSION_SANITY_SKIP = "1"
PV = "${LINUX_VERSION}+git${SRCPV}"

SRC_URI = "${REPO};branch=${BRANCH};protocol=https"
KERNEL_DEFCONFIG = "renesas_defconfig"

LIC_FILES_CHKSUM = "file://COPYING;md5=6bc538ed5bd9a7fc9398086aedcd7e46"

S = "${WORKDIR}/git"

# For generating defconfig
KCONFIG_MODE = "--alldefconfig"
KBUILD_DEFCONFIG = "defconfig"

FILESEXTRAPATHS:prepend:sparrow-hawk = "${TOPDIR}/../../firmware:"
SRC_URI:append:sparrow-hawk = " \
    file://sparrow_hawk.cfg \
    file://sparrow-hawk-enable-i2c3-i2c4.dtsi;subdir=git/arch/arm64/boot/dts/renesas/ \
    file://sparrow-hawk-reserved_memory_pcie_fw.dtsi;subdir=git/arch/arm64/boot/dts/renesas/ \
    file://0001-HACK-arm64-dts-renesas-r8a779g3-sparrow-hawk-Remove-.patch \
    file://sparrow-hawk-enable-j1-imx219.dtso;subdir=git/arch/arm64/boot/dts/renesas/ \
    file://sparrow-hawk-enable-j2-imx219.dtso;subdir=git/arch/arm64/boot/dts/renesas/ \
    file://sparrow-hawk-enable-j1-imx462.dtso;subdir=git/arch/arm64/boot/dts/renesas/ \
    file://sparrow-hawk-enable-j2-imx462.dtso;subdir=git/arch/arm64/boot/dts/renesas/ \
"
SRC_URI:append:sparrow-hawk = " \
    file://0001-WIP-pci-dwc-rcar-gen4-Add-support-to-load-firmware-f.patch \
"

KBUILD_DEFCONFIG:sparrow-hawk = "renesas_defconfig"
KERNEL_DEVICETREE:append:sparrow-hawk = " \
    renesas/r8a779g3-sparrow-hawk-fan-pwm.dtbo \
    renesas/r8a779g3-sparrow-hawk-rpi-display-2.dtbo \
    renesas/sparrow-hawk-enable-j1-imx219.dtbo \
    renesas/sparrow-hawk-enable-j2-imx219.dtbo \
    renesas/sparrow-hawk-enable-j1-imx462.dtbo \
    renesas/sparrow-hawk-enable-j2-imx462.dtbo \
"

do_compile:prepend:sparrow-hawk () {
    echo '#include "sparrow-hawk-enable-i2c3-i2c4.dtsi"' >>  ${S}/arch/arm64/boot/dts/renesas/r8a779g3-sparrow-hawk.dts
    echo '#include "sparrow-hawk-reserved_memory_pcie_fw.dtsi"' >>  ${S}/arch/arm64/boot/dts/renesas/r8a779g3-sparrow-hawk.dts
}

do_src_package_preprocess () {
    # Trim build paths from comments in generated sources to ensure reproducibility
    sed -i -e "s,${S}/,,g" \
            -e "s,${B}/,,g" \
        ${B}/drivers/video/logo/logo_linux_clut224.c \
        ${B}/drivers/tty/vt/consolemap_deftbl.c \
        ${B}/lib/oid_registry_data.c
}
addtask do_src_package_preprocess after do_compile before do_install

do_compile_kernelmodules:append () {
    if (grep -q -i -e '^CONFIG_MODULES=y$' ${B}/.config); then
        # 5.10+ kernels have module.lds that we need to copy for external module builds
        if [ -e "${B}/scripts/module.lds" ]; then
            install -Dm 0644 ${B}/scripts/module.lds ${STAGING_KERNEL_BUILDDIR}/scripts/module.lds
        fi
    fi
}

do_deploy:append() {
    # Remove the redundant device tree file (<device_tree>-<MACHINE>.dtb) that was created in the deploy directory
    for dtbf in ${KERNEL_DEVICETREE}; do
        dtb=`normalize_dtb "$dtbf"`
        dtb_ext=${dtb##*.}
        dtb_base_name=`basename $dtb .$dtb_ext`
        rm -f $deployDir/$dtb_base_name-${KERNEL_DTB_LINK_NAME}.$dtb_ext
    done
}

